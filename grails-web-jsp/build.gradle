configurations {
    jsp21.extendsFrom compile
}

dependencies {
    compile project(":grails-web-common")
    compile project(":grails-web-gsp")

    // Required for JSP support
    jsp21 'javax.servlet.jsp:jsp-api:2.1'
    provided 'javax.servlet:jsp-api:2.0', {
        exclude group: 'javax.servlet', module:'servlet-api'
    }

}

sourceSets {
    jsp21 {
        compileClasspath = files(configurations.jsp21, main.output)
        groovy.srcDirs = ['src/jsp21/groovy']
        resources.srcDirs = []
    }
}

jar {
    from compileJsp21Groovy.outputs.files
}

eclipse {
    classpath {
        plusConfigurations += [configurations.jsp21]
        file {
            whenMerged { classpath ->
                // move jsp-api-2.1 to the last one in entries so that it appears after -2.0
                def jsp21 = classpath.entries.find { entry -> entry?.path =~ /jsp-api-2.1.jar$/ }
                if (jsp21) {
                    classpath.entries.remove(jsp21)
                    classpath.entries << jsp21
                }
            }
        }
    }
}
