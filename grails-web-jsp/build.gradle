configurations {
    jsp21.extendsFrom compile
    optional
}

dependencies {
    compile project(":grails-web-common")
    compile project(":grails-web-gsp")

    // Required for JSP support
    jsp21 'javax.servlet.jsp:jsp-api:2.1'
    compile 'javax.servlet:jsp-api:2.0', {
        ext.notInPom = true
        exclude group: 'javax.servlet', module:'servlet-api'
    }

    // Required for tag library support
    runtime 'taglibs:standard:1.1.2'
    runtime "javax.servlet:jstl:1.1.2"
}

sourceSets {
    main {
        compileClasspath += files(configurations.optional)
    }
    jsp21 {
        compileClasspath = files(configurations.jsp21, main.output)
        groovy.srcDirs = ['src/jsp21/groovy']
        resources.srcDirs = []
    }
}

javadoc.classpath += files(configurations.optional)
jar {
    from compileJsp21Groovy.outputs.files
}

eclipse {
    classpath {
        plusConfigurations += configurations.jsp21
        file {
            whenMerged { classpath ->
                // move jsp-api-2.1 to the last one in entries so that it appears after -2.0
                def jsp21 = classpath.entries.find { entry -> entry?.path =~ /jsp-api-2.1.jar$/ }
                if (jsp21) {
                    classpath.entries.remove(jsp21)
                    classpath.entries << jsp21
                }
            }
        }
    }
}
